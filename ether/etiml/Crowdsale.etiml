pragma etiml ^0.1

interface token = interface 
  fun transfer(receiver : address, amount : uint)
end

contract Crowdsale = contract

  open Util
       
  public val beneficiary : address
  public val fundingGoal : ref uint
  public val amountRaised : ref uint
  public val deadline : ref uint
  public val price : ref uint
  public val tokenReward : ref option token
  public val balanceOf : map address uint256
  val fundingGoalReached : ref bool
  val crowdsaleClosed : ref bool

  event GoalReached(recipient : address, totalAmountRaised : uint)
  event FundTransfer(backer : address, amount : uint, isContribution : bool)

  (**
   * Constructor function
   *
   * Setup the owner
   *)
  fun init (
        ifSuccessfulSendTo : address,
        fundingGoalInEthers : uint,
        durationInMinutes : uint,
        etherCostOfEachToken : uint,
        addressOfTokenUsedAsReward : address
    ) using 8.0 =
    beneficiary := ifSuccessfulSendTo;
    fundingGoal := fundingGoalInEthers * 1 ether;
    deadline := now + durationInMinutes * 1 minutes;
    price := etherCostOfEachToken * 1 ether;
    tokenReward := token(addressOfTokenUsedAsReward)

  (**
   * Fallback function
   *
   * The function without name is the default function that is called whenever anyone sends funds to a contract
   *)
  fun default () payable using 18.0 =
    require(not crowdsaleClosed);
    let amount = msg.value;
    modify balanceOf[msg.sender] += amount;
    modify amountRaised += amount;
    transfer(valOf tokenReward, msg.sender, amount / price);
    FundTransfer(msg.sender, amount, true)

  fun afterDeadline k = if now >= deadline then k () else throw endif

  (**
   * Check if goal was reached
   *
   * Checks if the goal or time limit has been reached and ends the campaign
   *)
  fun checkGoalReached () guarded_by afterDeadline using 18.0 =
    if amountRaised >= fundingGoal then
      fundingGoalReached := true;
      GoalReached(beneficiary, amountRaised)
    else
      crowdsaleClosed := true
    endif


  (**
    * Withdraw the funds
    *
    * Checks to see if goal or time limit has been reached, and if so, and the funding goal was reached,
    * sends the entire amount to the beneficiary. If goal was not reached, each contributor can withdraw
    * the amount they contributed.
    *)
  fun safeWithdrawal () guarded_by afterDeadline using 23.0 =
    if not fundingGoalReached then
      let amount = balanceOf[msg.sender];
      set balanceOf[msg.sender] 0;
      if amount > 0 then
        if send(msg.sender, amount) then
          FundTransfer(msg.sender, amount, false)
        else 
          set balanceOf[msg.sender] amount
        endif
      endif
    endif;
    if fundingGoalReached && beneficiary = msg.sender then
      if send(beneficiary, amountRaised) then
        FundTransfer(beneficiary, amountRaised, false)
      else 
        (* If we fail to send the funds to beneficiary, unlock funders balance *)
        fundingGoalReached := false
      endif
    endif

end    
