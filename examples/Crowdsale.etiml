pragma etiml ^0.1

interface token = sig
  fun transfer(receiver : address, amount : uint)
end

contract Crowdsale = struct

  open Util
       
  public state beneficiary : ref address
  public state fundingGoal : ref uint
  public state amountRaised : ref uint
  public state deadline : ref uint
  public state price : ref uint
  public state tokenReward : ref address
  public state balanceOf : map address uint256
  state fundingGoalReached : ref bool
  state crowdsaleClosed : ref bool

  event GoalReached(recipient : address, totalAmountRaised : uint)
  event FundTransfer(backer : address, amount : uint, isContribution : bool)

  (**
   * Constructor function
   *
   * Setup the owner
   *)
  fun init (
        ifSuccessfulSendTo : address,
        fundingGoalInEthers : uint,
        durationInMinutes : uint,
        etherCostOfEachToken : uint,
        addressOfTokenUsedAsReward : address
    ) using _ =
    beneficiary := ifSuccessfulSendTo;
    fundingGoal := fundingGoalInEthers * 1 ether;
    deadline := now + durationInMinutes * 1 minutes;
    price := etherCostOfEachToken * 1 ether;
    tokenReward := addressOfTokenUsedAsReward

  (**
   * Fallback function
   *
   * The function without name is the default function that is called whenever anyone sends funds to a contract
   *)
  fun default () payable using _ =
    require(not crowdsaleClosed);
    let amount = msg.value;
    modify balanceOf[msg.sender] += amount;
    modify amountRaised += amount;
    transfer(valOf tokenReward, msg.sender, amount / price);
    FundTransfer(msg.sender, amount, true)

  (* fun afterDeadline k = if now >= deadline then k () else throw end *)

  (**
   * Check if goal was reached
   *
   * Checks if the goal or time limit has been reached and ends the campaign
   *)
  fun checkGoalReached () guarded_by afterDeadline using _ =
    if not (now >= deadline) then throw end;
    if amountRaised >= fundingGoal then
      fundingGoalReached := true;
      GoalReached(beneficiary, amountRaised)
    else
      crowdsaleClosed := true
    end


  (**
    * Withdraw the funds
    *
    * Checks to see if goal or time limit has been reached, and if so, and the funding goal was reached,
    * sends the entire amount to the beneficiary. If goal was not reached, each contributor can withdraw
    * the amount they contributed.
    *)
  fun safeWithdrawal () guarded_by afterDeadline using _ =
    if not fundingGoalReached then
      let amount = balanceOf[msg.sender];
      set balanceOf[msg.sender] 0;
      if amount > 0 then
        if send(msg.sender, amount) then
          FundTransfer(msg.sender, amount, false)
        else 
          set balanceOf[msg.sender] amount
        end
      end
    end;
    if fundingGoalReached && beneficiary = msg.sender then
      if send(beneficiary, amountRaised) then
        FundTransfer(beneficiary, amountRaised, false)
      else 
        (* If we fail to send the funds to beneficiary, unlock funders balance *)
        fundingGoalReached := false
      end
    end

end    
